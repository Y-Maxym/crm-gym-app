plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.openapi.generator' version "$openApiGeneratorVersion"
}

group = 'com.gym.crm.app'
version = '0.0.1-SNAPSHOT'

application {
    mainClassName = 'com.gym.crm.app.CrmGymApplication'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "org.apache.commons:commons-text:$apacheCommons"

    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework:spring-orm'
    implementation "org.hibernate:hibernate-core:$hibernateCore"

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.24'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['com/gym/crm/app/mapper/**', 'org/openapitools/**', 'com/gym/crm/app/rest/model'])
                })
        )
    }
}

openApiGenerate {
    inputSpec = "$rootDir/src/main/resources/gym-crm-api.yml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.toString()
    generatorName = "spring"
    modelPackage = "com.gym.crm.app.rest.model"
    configOptions = [
            useSpringBoot3: "true",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}